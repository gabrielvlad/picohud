//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath "com.github.breadmoirai:github-release:2.4.1"
	}
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
}

archivesBaseName = project.archives_base_name

def mc_major = '1.19'
def mc = mc_major + '.2'
def default_branch = '1.19'

def origVersion = project.version
project.version = project.version + "+" + mc_major
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.cafeteria.dev/releases" }
	maven { url "https://maven.shedaniel.me/" } // Seasons uses REI
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.qm) { classifier "intermediary-v2" }
	modImplementation libs.ql
	modImplementation libs.qsl
	modImplementation libs.qfapi

	compileOnly libs.nightConfig
	compileOnly libs.nightConfigToml
 	api libs.quiltConfig

	include libs.nightConfig
	include libs.nightConfigToml
	include libs.quiltConfig

	// Compat //
	modCompileOnly libs.seasons
	modLocalRuntime libs.seasons
}

processResources {
	final Map<String, String> map = [
		"version"        : version.toString(),
		"mc"             : libs.versions.mc.get(),
		"ql"             : libs.versions.ql.get(),
		"qsl"            : libs.versions.qsl.get(),
		"fapi"          : libs.versions.fapi.get(),
		"qfapi"          : libs.versions.qfapi.get(),
		"seasons"          : libs.versions.seasons.get(),
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}


import com.google.gson.JsonObject;
import com.google.gson.JsonElement;
import com.google.gson.JsonArray;
import com.google.gson.JsonParser;
import com.google.gson.GsonBuilder;
import java.nio.file.Files;
import java.nio.file.FileSystems;
import java.nio.file.Path;

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

def gson = new GsonBuilder().setPrettyPrinting().create()


tasks.register("noQuiltConfigInQmj") {
	group "fabric"
	dependsOn "remapJar"
	inputs.files(remapJar.outputs.files)
	doLast {
		for (final def file in getInputs().getFiles()) {
			for (final def root in FileSystems.newFileSystem(file.toPath()).getRootDirectories()) {
				Path qmj = root.resolve("quilt.mod.json")
				JsonObject json = JsonParser.parseString(qmj.text).getAsJsonObject();
				JsonObject loaderObject = json.get("quilt_loader")
				List<JsonElement> jars = loaderObject.get("jars").getAsJsonArray().toList()
				jars.removeIf( it2 -> it2.getAsString() == "META-INF/jars/quilt-config-1.0.0-beta.6.jar")
				JsonArray outJars = new JsonArray()
				jars.forEach(outJars::add)
				loaderObject.add("jars", outJars)
				Files.writeString(qmj, gson.toJson(json))
			}
		}
	}
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'picohud'
		versionNumber = project.version
		versionName = origVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [mc, '1.19.3', '1.19.4']
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
			optional.project "fabric-seasons"
		}
		changelog = "Changelog: https://github.com/sisby-folk/picohud/releases/tag/v" + origVersion
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "picohud"
		tagName 'v' + origVersion
		releaseName origVersion
		targetCommitish default_branch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}
