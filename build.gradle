//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath "com.github.breadmoirai:github-release:2.4.1"
	}
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
}

archivesBaseName = project.archives_base_name

def mc_major = '1.19'
def mc = mc_major + '.2'
def default_branch = '1.19'

def origVersion = project.version
project.version = project.version + "+" + mc_major
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.cafeteria.dev/releases" }
	maven { url "https://maven.shedaniel.me/" } // Seasons uses REI
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.qm) { classifier "intermediary-v2" }
	modImplementation libs.ql
	modImplementation libs.qfapi

	implementation libs.quiltConfig
	implementation libs.nightConfig
	implementation libs.nightConfigToml

	shadow libs.quiltConfig
	shadow libs.nightConfig
	shadow libs.nightConfigToml

	// Compat //
	modCompileOnly libs.seasons
	modLocalRuntime libs.seasons
}

processResources {
	final Map<String, String> map = [
		"version": version.toString(),
		"mc"     : libs.versions.mc.get(),
		"ql"     : libs.versions.ql.get(),
		"fapi"   : libs.versions.fapi.get(),
		"qfapi"  : libs.versions.qfapi.get(),
		"seasons": libs.versions.seasons.get(),
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

shadowJar {
	relocate 'org.quiltmc.config', 'folk.sisby.picohud.repackage.quiltconfig'
	relocate 'com.electronwill.nightconfig', 'folk.sisby.picohud.repackage.nightconfig'
	dependencies {
		include dependency(libs.nightConfig.get())
		include dependency(libs.nightConfigToml.get())
		include dependency(libs.quiltConfig.get())
	}
	minimize()
}

remapJar {
	dependsOn(shadowJar)
	input.set(shadowJar.archivePath)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'picohud'
		versionNumber = project.version
		versionName = origVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [mc, '1.19.3', '1.19.4']
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
			optional.project "fabric-seasons"
		}
		changelog = "Changelog: https://github.com/sisby-folk/picohud/releases/tag/v" + origVersion
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "picohud"
		tagName 'v' + origVersion
		releaseName origVersion
		targetCommitish default_branch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}
